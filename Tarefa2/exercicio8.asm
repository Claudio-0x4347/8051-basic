ORG 00H
SJMP SETUP

; =========== SUBROTINA PARA CONFIGURAR OS PERIFERICOS =============
ORG 30H
SETUP: 		CLR P2.7		; INICIA COM A LAMPADA DESLIGADA
		MOV TMOD, #20H		; COLOCA O TIMER 1 EM MODO 2
		MOV TH1, #0F4H		; VALOR DE RECARGA PARA 2400 BITS/S
		MOV SCON, #50H		; SERIAL MODO 1 (SM0=0 SM1=1) E REN=1
		SETB TR1		; DISPARA A CONTAGEM DO TIMER 1

MAIN:		MOV DPTR, #MENU
		LCALL SERIAL_WR		; ESCREVE O MENU NA SERIAL E AGUARDA INTERRUPÇÃO
		MOV DPTR, #MSG		; JÁ DEIXA NO DPTR O ENDEREÇO DA MENSAGEM
SERIAL:		JNB RI, $
		MOV A, SBUF		; MOVE PARA O ACUMULADOR O VALOR RECEBIDO
		JZ RET1			; VERIFICA SE NÃO RECEBEU NULO
		CJNE A, #59H, IS_N	; COMPARA SE É Y (0x59H ASCII)
		SETB P2.7		; LIGA A LAMPADA 2
		SJMP RET1

IS_N:		CJNE A, #4EH, ISNOT_YN	; COMPARA SE NÃO É N (0x4E ASCII)
		CLR P2.7		; DESLIGA A LAMPADA 2
		SJMP RET1

ISNOT_YN:	LCALL SERIAL_WR		; ENVIA MENSAGEM DE ALERTA
RET1:		CLR RI			; RESETA O BIT DE RECEPÇÃO
		SJMP SERIAL		; ESPERA RECEBER UM CARACTER NA SERIAL



; =========== SUBROTINA PARA ENVIO DE MENSAGENS ====================
SERIAL_WR:	MOV R2, #0
NEXT:		MOV A, R2
		MOVC A, @A+DPTR
		CJNE A, #0FFH, SEND	; DETECTA O CARACTER DE FINAL DE TEXTO
		RET

SEND:		MOV SBUF, A		; INICIA A TRANSMISSÃO DO CARACTER
		JNB TI, $		; AGUARDA TRANSMISSÃO
		CLR TI			; LIMPA FLAG DE TRANSMISSÃO PARA O PROXIMO DADO
		INC R2
		SJMP NEXT		; VAI PARA O PROXIMO MARACTER DA MENSAGEM

; ============ MENSAGENS A SER ENVIADAS =============================
MENU:	DB 'Y - LIGAR LAMPADA', 0DH, 'N - DESLIGAR A LAMPADA', 0DH, 0FFH
MSG:	DB 'DIGITE Y OU N', 0DH, 0FFH
END