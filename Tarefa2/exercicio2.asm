ORG 00H
SJMP SETUP

ORG 03H
LJMP MOTOR_ESQUERDA

ORG 13H
LJMP MOTOR_DIREITA

; =========== SUBROTINA PARA CONFIGURAR OS PERIFERICOS =============
ORG 30H
SETUP: 		MOV P2, #11H	; INICIALIZA OS PASSOS DO MOTOR
		MOV IE, #85H	; EA = 1, EX1 = 1, EX0 = 1
		MOV TCON, #05H	; IT1 = 1 IT0 = 1 INTERRUPÇÕES POR TRANSIÇÃO

MAIN: 		SJMP $		; ESPERA AS INTERRUPÇÕES

; =========== SUBROTINA DE MOVIMENTAÇÃO DO MOTOR ===================
MOTOR_DIREITA:	MOV R2, #36	; 36 ROTAÇÕES NECESSÁRIAS PARA 180º
		MOV A, P2
ROT_D:		RR A
		MOV P2, A	; ATUALIZA O MOTOR
		LCALL DELAY
		DJNZ R2, ROT_D
		RETI

MOTOR_ESQUERDA: MOV R2, #150	; 150 PASSOS
		MOV A, P2
ROT_E:		RL A
		MOV P2, A	; ATUALIZA O MOTOR
		LCALL DELAY
		DJNZ R2, ROT_E
		RETI

; =========== SUBROTINA DE ATRASO ==================================
DELAY:		MOV R0, #130
RECARGA:	MOV R1, #250
		DJNZ R1, $
		DJNZ R0, RECARGA
		RET
END
