PAVIMENTO	EQU	P1

IN1		EQU	P0.0		; IN1 NO DRIVE L298
IN2		EQU 	P0.1		; IN2 NO DRIVE L298

PULSOS		EQU	R0		; CONTADOR DE PULSOS
DESTINO		EQU 	R7		; ARMAZENA O PAVIMENTO DESTINO

ORG 00H
LJMP INICIO

ORG 0003H				; INTERRUPÇÃO EXTERNA 0
LJMP		EXTERNA0

ORG 000BH		; INTERRUPÇAO DO TIMER 0
RETI
LJMP TIMER0		; SUB-ROTINA QUE PROCESSA O ESTOURO DE UMA CONTAGEM

ORG 30H
INICIO: 	MOV PAVIMENTO, #01	; COMEÇA COM O ELEVADOR NO PAVIMENTO 1
		; CONFIGURAÇÕES INICIAIS DAS INTERRUPÇÕES, TIMERS E SERIAL
		MOV IE, #83H		; HABILITA A INTERRUPÇÃO PELO TIMER 0 E EXTERNA 0
		MOV IP, #02H		; COLOCA PRIORIDADE NA INTERRUPÇÃO COM TIMER 0
		MOV TMOD, #21H		; TIMER ZERO EM MODO 1 E TIMER 1 MODO 2
		MOV TCON, #00H		; INTERRUPÇÃO EXTERNA 0 POR NÍVEL
		; CONFIGURAÇÃO DA SERIAL
		MOV SCON, #40H		; CONFIGURA SERIAL MODO 1(UART 8BITS)
		MOV TL1, #0FAH		; BAUNDRATE 4800 COM TIMER 1
		MOV TH1, #0FAH
		SETB TR1		; DISPARAM CONTAGEM
		MOV DESTINO, PAVIMENTO	; COMEÇA NO PAVIMENTO INICIAL
		LCALL WR_SERIAL		; COMEÇA IMPRIMINDO A POSICAO INICIAL
		SJMP $			; AGUARDA INTERRUPÇÕES
		
ATRASO: 	MOV PULSOS, #72
		SETB TR0		; DISPARA CONTAGEM
V3:		MOV TH0, #05H
		MOV TL0, #0FFH
		JNB TF0,$
		CLR TF0
		DJNZ PULSOS,V3		; VERIFICA 72 CONTAGENS DE 69.44ms
		CLR TR0			; DESABILITA CONTAGEM
		RET

; ====== TRATAMENTO DA INTERRUPÇÃO EXTERNA ========
EXTERNA0:	LCALL TECLADO		; ARMAZENA O PEDIDO EM DESTINO
		MOV A, PAVIMENTO
		MOV B, DESTINO
		CJNE A, B, MOVIMENTA	; MOVIMENTA APENAS QUANDO DESTINO E PAVIMENTO SAO 							DIFERENTES
		RETI
		
MOVIMENTA:	LCALL MOTOR		; ATIVA O MOTOR
UP:		LCALL ATRASO		; DELAY 5S
		LCALL LED		; ATUALIZA INDICADOR
		LCALL WR_SERIAL
		MOV A, PAVIMENTO
		MOV B, DESTINO
		CJNE A, B, UP		; LOOP ATÉ O DESTINO
		LCALL MOTOR_OFF
		RETI

; ======== SUBROTINA PARA MOVIMENTAR O LED ========
LED: 		MOV A, PAVIMENTO	; VERIFICA SE JA CHEGOU NO DESTINO
		MOV B, DESTINO
		CJNE A, B, TRATAMENTO
		RET

TRATAMENTO:	MOV B, DESTINO		; DETERMINA QUAL O SENTIDO DA MOVIMENTAÇÃO
		MOV A, PAVIMENTO
		SUBB A, B		; COMPARA COM A POSIÇÃO ATUAL
		JNB C, L_DOWN		; C=0 DESTINO < PAVIMENTO ATUAL
		CLR C			; LIMPA A FLAG PARA PROXIMOS TRATAMENTOS

L_UP:		MOV A, PAVIMENTO
		RL A			; ATUALIZA O PAVIMENTO
		JMP F5

L_DOWN:		MOV A, PAVIMENTO
		RR A			; ATUALIZA O PAVIMENTO

F5:		MOV PAVIMENTO, A
		RET

; =========== SUBROTINA MOVIMENTACAO MOTOR CC ==============
MOTOR:		MOV A, PAVIMENTO	; VERIFICA O SENTIDO DA ROTAÇÃO
		MOV B, DESTINO
		SUBB A, B
		JNB C, M_DOWN		; C=0 DESTINO < PAVIMENTO ATUAL
		CLR C			; LIMPA A FLAG PARA PROXIMOS TRATAMENTOS

M_UP:		CLR IN1			; MOTOR CC SENTIDO INVERSO
		SETB IN2
		JMP ON

M_DOWN:		SETB IN1		; MOTOR CC SENTIDO DIRETO
		CLR IN2

ON:		MOV P2, #60H		; SELECIONA A SAÍDA DO MOTOR
		CLR P3.6		; LIGA O MOTOR CC
		CLR P3.7
		RET

MOTOR_OFF:	CPL IN1
		SETB P3.6
		SETB P3.7
		RET
		
; =========== SUBROTINA PARA COMUNICAÇÃO SERIAL ============
WR_SERIAL:	MOV R2, #00H		; INICIALIZA O DESLOCAMENTO NA MENSAGEM
F1:		MOV A, R2
		LCALL APONTA_PAV	; SUBROTINA PARA AJUSTAR O DPTR
		MOVC A, @A+DPTR
		MOV SBUF, A		; INICIA A TRANSMISSÃO DO CARACTER
		JNB TI, $		; AGUARDA TRANSMISSÃO
		CLR TI			; LIMPA FLAG DE TRANSMISSÃO PARA O PROXIMO DADO
		INC R2
		CJNE R2, #14H, F1
		RET
		
APONTA_PAV:	JNB P1.0, PAV1
		MOV DPTR, #MSG0		; SELECIONA MENSAGEM 0
		RET
PAV1:		JNB P1.1, PAV2
		MOV DPTR, #MSG1		; SELECIONA MENSAGEM 1
		RET
PAV2:		JNB P1.2, PAV3
		MOV DPTR, #MSG2		; SELECIONA MENSAGEM 2
		RET
PAV3:		JNB P1.3, PAV4
		MOV DPTR, #MSG3		; SELECIONA MENSAGEM 3
		RET
PAV4:		JNB P1.4, PAV5
		MOV DPTR, #MSG4		; SELECIONA MENSAGEM 4
		RET
PAV5:		JNB P1.5, PAV6
		MOV DPTR, #MSG5		; SELECIONA MENSAGEM 5
		RET
PAV6:		JNB P1.6, PAV7
		MOV DPTR, #MSG6		; SELECIONA MENSAGEM 6
		RET
PAV7:		MOV DPTR, #MSG7		; SELECIONA MENSAGEM 7
		RET

; ============== SUBROTINA PARA TECLADO ====================
TECLADO:	MOV DPTR, #8000H
		MOVX A, @DPTR		; RECUPERAR O VALOR DO TECLADO
		LCALL PROCESSA
		RET

PROCESSA:	CJNE A, #00H, TECLA1	; SE VALOR NA PORTA FOR 0
		MOV DESTINO, #01H
		RET
TECLA1:		CJNE A, #01H, TECLA2	; SE VALOR NA PORTA FOR 1
		MOV DESTINO, #02H
		RET
TECLA2:		CJNE A, #02H, TECLA3	; SE VALOR NA PORTA FOR 2
		MOV DESTINO, #04H
		RET
TECLA3:		CJNE A, #03H, TECLA4	; SE VALOR NA PORTA FOR 3
		MOV DESTINO, #08H
		RET
TECLA4:		CJNE A, #04H, TECLA5	; SE VALOR NA PORTA FOR 4
		MOV DESTINO, #10H
		RET
TECLA5:		CJNE A, #05H, TECLA6	; SE VALOR NA PORTA FOR 5
		MOV DESTINO, #20H
		RET
TECLA6:		CJNE A, #06H, TECLA7	; SE VALOR NA PORTA FOR 6
		MOV DESTINO, #40H
		RET
TECLA7:		CJNE A, #07H, TECLAN	; SE VALOR NA PORTA FOR 7
		MOV DESTINO, #80H
TECLAN:		RET

; =========== TABELA COM O LITERAL PARA O LCD ==============
MSG0:	DB 'PAVIMENTO PRIVATIVO', 0DH
MSG1:	DB 'PAVIMENTO 1        ', 0DH
MSG2: 	DB 'PAVIMENTO 2        ', 0DH
MSG3:	DB 'PAVIMENTO 3        ', 0DH
MSG4:	DB 'PAVIMENTO 4        ', 0DH
MSG5:	DB 'PAVIMENTO 5        ', 0DH
MSG6:	DB 'PAVIMENTO 6        ', 0DH
MSG7:	DB 'PAVIMENTO 7        ', 0DH
END
